apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.sentry.android.gradle'

// A properties file is located at projectFolder/keys.properties
def keysPropertiesFile = rootProject.file("keys.properties")
def keys = new Properties()
keys.load(new FileInputStream(keysPropertiesFile))

android {
    defaultConfig {
        applicationId "com.marknkamau.justjava"

        versionCode 14
        versionName "2.2.1"

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "com.marknkamau.justjava.testUtils.TestApplicationRunner"

        buildConfigField("String", "googleClientId", '"565628649475-4vp8dm790n2kuuhqpu3o662e9odht703.apps.googleusercontent.com"')
        buildConfigField("String", "sentryDsn", keys['SENTRY_DSN'])
        resValue "string", "google_maps_key", keys['GOOGLE_MAPS_API_KEY'] // Generates @strings/google_maps_key
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        debug {
            storeFile file("../keystore/justjava-debug.jks")
            storePassword "justjava-debug"
            keyAlias "key0"
            keyPassword "justjava-debug"
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation project(':core')

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.72"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.20.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.3.8"

    //Android libraries
    implementation "com.google.android.material:material:1.1.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-beta8"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    // Firebase libraries
    implementation "com.google.firebase:firebase-messaging:20.2.3"

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.2.0"

    // Room
    implementation "androidx.room:room-runtime:2.2.5"
    implementation "androidx.room:room-ktx:2.2.5"
    kapt "androidx.room:room-compiler:2.2.5"

    // DI
    implementation "org.koin:koin-android:2.1.5"
    implementation "org.koin:koin-androidx-viewmodel:2.1.5"

    // Other libraries
    implementation "com.squareup.picasso:picasso:2.71828"
    implementation "com.jakewharton.timber:timber:4.7.1"
    implementation "com.facebook.shimmer:shimmer:0.5.0"
    implementation "com.google.android.gms:play-services-auth:18.1.0"
    implementation "com.google.android.gms:play-services-maps:17.0.0"
    implementation "com.google.android.libraries.places:places:2.3.0"
    implementation "io.sentry:sentry-android:1.7.27"

    // Test libraries
    testImplementation "junit:junit:4.13"
    testImplementation "io.mockk:mockk:1.9.2"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.5"
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation "io.mockk:mockk:1.9.2"
    androidTestImplementation "io.mockk:mockk-android:1.9.2"
    androidTestImplementation "androidx.test:runner:1.3.0-rc01"
    androidTestImplementation "androidx.test:rules:1.3.0-rc01"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0-rc01"
    androidTestImplementation "androidx.test.espresso:espresso-intents:3.3.0-rc01"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:3.3.0-rc01"
    androidTestImplementation "androidx.test:core:1.2.0"
}

apply plugin: 'com.google.gms.google-services'
